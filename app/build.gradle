import com.vlsantillan.imagetoresult.buildsrc.Libs

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.vlsantillan.imagetoresult"
        minSdk 28
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "theme", "functionality"
    productFlavors {
        red {
            dimension "theme"
        }
        green {
            dimension "theme"
        }
        camera {
            dimension "functionality"
        }
        fileSystem {
            dimension "functionality"
        }
    }

    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavorName = variant.flavorName // e. g. Free
            if (flavorName == "redFileSystem") {
                outputFileName = "app-red-filesystem.apk"
            } else if (flavorName == "greenFileSystem") {
                outputFileName = "app-green-filesystem.apk"
            } else if (flavorName == "redCamera") {
                outputFileName = "app-red-built-in-camera.apk"
            } else if (flavorName == "greenCamera") {
                outputFileName = "app-green-built-in-camera.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation project(':domain')
    implementation project(':data')

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.Compose.ui
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.toolingPreview

    implementation Libs.AndroidX.Lifecycle.lifecycleKtx
    implementation Libs.AndroidX.Lifecycle.viewModelCompose
    implementation Libs.AndroidX.Activity.activityCompose
    implementation Libs.AndroidX.ConstraintLayout.constraintLayoutCompose
    implementation Libs.AndroidX.ConstraintLayout.constraintLayout
    implementation Libs.AndroidX.Navigation.navigationCompose
    implementation Libs.AndroidX.Navigation.navigationFragment
    implementation Libs.AndroidX.Navigation.navigationKtx

    implementation Libs.AndroidX.Camera.camera2
    implementation Libs.AndroidX.Camera.cameraView
    implementation Libs.AndroidX.Camera.cameraLifecycle

    implementation Libs.Accompanist.permission

    implementation Libs.MLKit.textRecognition
    implementation Libs.ExpressionEvaluator.evaluator

    implementation Libs.Hilt.hiltAndroid
    implementation Libs.Hilt.hiltNavigation
    kapt Libs.Hilt.hiltCompiler

    testImplementation Libs.Test.junit
    androidTestImplementation Libs.Test.Ext.junit
    androidTestImplementation Libs.Test.espressoCore
    androidTestImplementation Libs.AndroidX.Compose.uiTest

    debugImplementation Libs.AndroidX.Compose.tooling
    debugImplementation Libs.AndroidX.Compose.uiTestManifest
}